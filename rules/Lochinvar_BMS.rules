import org.openhab.core.model.script.ScriptServiceUtil

// ===================================================================================
// LOCHINVAR UNIFIED MASTER CONTROLLER
// Modes: 0=LEGACY, 1=BMS SETPOINT, 2=BMS FLOW (RATE)
// Updated: Delta-T Policing, Energy Integration, Active Governor Tracking
// ===================================================================================

// --- GLOBAL VARIABLES ---
var long last_demand_time = 0        // Tracks time of last VALID demand (PID > 5%)
var double last_firing_rate = 0.0    // Tracks Ramp Rate (Soft Start)
var boolean wwsd_active = false      // Tracks Warm Weather Shutdown State
var double lastSafeRate = 0.0        // Rate limiter for high limit jumps

// NEW TIMERS
var long safety_trip_start_time = 0  // For High Limit Ride Through
var long wwsd_timer_start = 0        // For WWSD Hysteresis (1 Hour)
var long last_dhw_end_time = 0      // Tracks when DHW cycle finished

// -----------------------------------------------------------------------------------
// RULE 0: SYNC LEGACY SWITCH (Compatibility Bridge)
// -----------------------------------------------------------------------------------
rule "Sync Control Mode to Legacy Switch"
when
    Item Boiler_Control_Mode changed
then
    if (Boiler_Control_Mode.state instanceof Number) {
        var int mode = (Boiler_Control_Mode.state as Number).intValue
        if (mode > 0) {
            if (Boiler_BMS_ENBLE.state != ON) Boiler_BMS_ENBLE.postUpdate(ON)
        } else {
            if (Boiler_BMS_ENBLE.state != OFF) Boiler_BMS_ENBLE.postUpdate(OFF)
        }
    }
end

rule "Initialize Control Mode"
when
    System started
then
    if (Boiler_Control_Mode.state == NULL || Boiler_Control_Mode.state == UNDEF) {
        Boiler_Control_Mode.postUpdate(0) 
    }
end

// -----------------------------------------------------------------------------------
// RULE 1: HEARTBEAT & MODE ENFORCEMENT (Runs every minute)
// -----------------------------------------------------------------------------------
rule "Boiler Heartbeat and Mode Enforcement"
when
    Time cron "0 * * ? * *" 
then
    if (Boiler_Test_Repeat.state == ON) return;
    if (System_Maintenance_Mode.state == ON) return;
    if (Boiler_Control_Mode.state == NULL || Boiler_Control_Mode.state == UNDEF) return;

    val int mode = (Boiler_Control_Mode.state as Number).intValue
    val int lockoutCode = if(Boiler_Lockout_Code.state instanceof Number) (Boiler_Lockout_Code.state as Number).intValue else 32767
    
    // Check DHW Status (19 = DHW Mode)
    val int statusCode = if(Boiler_Status_Code.state instanceof Number) (Boiler_Status_Code.state as Number).intValue else 0
    
    if (lockoutCode != 32767) {
        logWarn("Boiler.Safety", "HARD LOCKOUT DETECTED (Code " + lockoutCode + "). BMS Control Paused.")
        return;
    }

    // Don't mess with config registers if DHW is running
    if (statusCode == 19) {
        return; 
    }

    if (mode == 1 || mode == 2) {
        Lochinvar_40001_Control_Mode.sendCommand(5)
        if (CFH1.state == ON) CFH1.sendCommand(OFF)
        if (CFH2.state == ON) CFH2.sendCommand(OFF)
    } 
    else {
        Lochinvar_40001_Control_Mode.sendCommand(0)
        var int currentEn = -1
        if (Lochinvar_40002_Boiler_Enable_Cmd.state instanceof Number) currentEn = (Lochinvar_40002_Boiler_Enable_Cmd.state as Number).intValue
        if (currentEn != 0) Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(0)
        
        if (Lochinvar_40003_Rate_Command.state instanceof Number) {
            if ((Lochinvar_40003_Rate_Command.state as Number).intValue != 0) Lochinvar_40003_Rate_Command.sendCommand(0)
        }
    }
end

// -----------------------------------------------------------------------------------
// RULE 2: BMS SETPOINT MANAGER (MODE 1)
// -----------------------------------------------------------------------------------
rule "Boiler Setpoint Manager (Mode 1)"
when
    Time cron "0/10 * * * * ?"
then
    if (Boiler_Control_Mode.state != 1) return;
    if (Boiler_Test_Repeat.state == ON) return;
    if (System_Maintenance_Mode.state == ON) return;

    // DHW CHECK: If Boiler is in DHW Mode (19), Exit Immediately.
    val int statusCode = if(Boiler_Status_Code.state instanceof Number) (Boiler_Status_Code.state as Number).intValue else 0
    if (statusCode == 19) {
        // Ensure Enable is held High so we don't kill the cycle
        if (Lochinvar_40002_Boiler_Enable_Cmd.state != 1) {
            Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(1)
        }
        return;
    }

    var double targetTemp = 0.0
    if (Boiler_Target_Temp.state instanceof Number) {
        targetTemp = (Boiler_Target_Temp.state as Number).doubleValue
    }
    
    var boolean isBlocked = (Boiler_Block_Active.state == ON)
    
    // SCALING FOR MODE 1 (Setpoint)
    val double boiler_min_setpoint = 70.0
    val double boiler_max_setpoint = 180.0
    var double finalPercent = 0.0
    var int desiredEnable = 0
    
    if (!isBlocked && targetTemp >= boiler_min_setpoint) {
        var double numerator = targetTemp - boiler_min_setpoint
        var double denominator = boiler_max_setpoint - boiler_min_setpoint
        if (denominator == 0) denominator = 1.0 
        finalPercent = (numerator / denominator) * 100.0
        
        if (finalPercent > 100.0) finalPercent = 100.0
        if (finalPercent < 0.0) finalPercent = 0.0
        
        desiredEnable = 1
        last_demand_time = now.millis
    }
    else {
        finalPercent = 0.0
        if (last_demand_time > 0 && (now.millis - last_demand_time) < 60000) {
            desiredEnable = 1
        } else {
            desiredEnable = 0
        }
    }

    var int currentEn = if(Lochinvar_40002_Boiler_Enable_Cmd.state instanceof Number) (Lochinvar_40002_Boiler_Enable_Cmd.state as Number).intValue else -1
    if (currentEn != desiredEnable) {
        Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(desiredEnable)
        Thread::sleep(100)
    }
    
    var int payload = finalPercent.intValue
    var int currentPayload = if(Lochinvar_40003_Rate_Command.state instanceof Number) (Lochinvar_40003_Rate_Command.state as Number).intValue else -1
    
    if (desiredEnable == 1 && currentPayload != payload) {
         Lochinvar_40003_Rate_Command.sendCommand(payload)
    }
    else if (desiredEnable == 0 && currentPayload != 0) {
         Lochinvar_40003_Rate_Command.sendCommand(0)
    }
    Boiler_Final_Command.postUpdate(payload)
end

// -----------------------------------------------------------------------------------
// RULE 3: BMS FLOW/RATE CONTROLLER (MODE 2) - Runs every 5s
// -----------------------------------------------------------------------------------
rule "Hybrid Boiler PID Loop (Mode 2)"
when
    Time cron "0/5 * * * * ?"
then
    if (Boiler_Control_Mode.state != 2) return;
    if (Boiler_Test_Repeat.state == ON) return;
    if (System_Maintenance_Mode.state == ON) {
        Boiler_PID_Output.postUpdate(0)
        return;
    }
    
    // 1. CHECK DHW STATUS (Status Code 19 = DHW)
    val int statusCode = if(Boiler_Status_Code.state instanceof Number) (Boiler_Status_Code.state as Number).intValue else 0
    
    // TRACK DHW TRANSITION
    if (statusCode == 19) {
        last_dhw_end_time = now.millis
        
        if (Lochinvar_40002_Boiler_Enable_Cmd.state != 1) {
            Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(1)
        }
        if (Lochinvar_40003_Rate_Command.state != 100) {
            Lochinvar_40003_Rate_Command.sendCommand(100)
        }
        logInfo("Boiler.DHW", "DHW Active (Code 19). BMS Paused.")
        return;
    }

    // 1. FETCH SENSORS & MEMORY
    var double mem_pid_integral = 0.0
    if (Boiler_Pid_Integral.state instanceof Number) mem_pid_integral = (Boiler_Pid_Integral.state as Number).doubleValue

    val double supplyTemp  = if(System_Supply_Temp.state instanceof Number) (System_Supply_Temp.state as Number).doubleValue else 140.0
    val double targetTemp  = if(Boiler_Target_Temp.state instanceof Number) (Boiler_Target_Temp.state as Number).doubleValue else 0.0
    val double outletTemp  = if(Boiler_Outlet_Temp.state instanceof Number) (Boiler_Outlet_Temp.state as Number).doubleValue else 140.0
    val double inletTemp   = if(Boiler_Inlet_Temp.state instanceof Number) (Boiler_Inlet_Temp.state as Number).doubleValue else 100.0
    val double pumpSpeed   = if(Boiler_Pump_Speed.state instanceof Number) (Boiler_Pump_Speed.state as Number).doubleValue else 0.0
    
    // --- POST-DHW CHILL OUT (IDLE AT 10%) ---
    // If DHW just finished (< 90 seconds ago), hold at 10% to prevent shutdown logic.
    if ((now.millis - last_dhw_end_time) < 90000) {
        logInfo("Boiler.DHW", "Post-DHW Chill Out Active. Holding Rate at 10%.")
        
        if (Lochinvar_40002_Boiler_Enable_Cmd.state != 1) {
            Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(1)
        }
        
        // Force Rate to 10% (Min Fire) to keep the flame and pump alive
        if (Lochinvar_40003_Rate_Command.state != 10) {
            Lochinvar_40003_Rate_Command.sendCommand(10)
        }
        return;
    }

    // --- UPDATED WWSD LOGIC ---
    var double effective_outdoor = 32.0 
    if (Outdoor_Temperature.state instanceof Number) effective_outdoor = (Outdoor_Temperature.state as Number).doubleValue
    else if (Weather_Current_Temperature.state instanceof Number) effective_outdoor = (Weather_Current_Temperature.state as Number).doubleValue

    var int hour = now.getHourOfDay
    var boolean sun_block_active = (hour >= 16 && hour < 18)

    if (!sun_block_active) {
        if (effective_outdoor >= 70.0) {
            if (wwsd_timer_start == 0) wwsd_timer_start = now.millis
            if ((now.millis - wwsd_timer_start) > 3600000) {
                if (!wwsd_active) {
                    wwsd_active = true
                    logInfo("Boiler.WWSD", "WWSD Activated (1 Hour Sustained Warmth)")
                }
            }
        } 
        else if (effective_outdoor <= 67.0) {
            wwsd_active = false
            wwsd_timer_start = 0
        }
    } else {
        if (effective_outdoor <= 67.0) wwsd_active = false
    }

    var double base_Kp = 3.0
    if (Boiler_Kp.state instanceof Number) base_Kp = (Boiler_Kp.state as Number).doubleValue
    var double Ki = 0.05 
    
    // 2. CALL DETECTION
    var boolean callActive = false
    if (LRZV.state == ON || GBrZV.state == ON || BMTZV.state == ON || GAZV.state == ON || MRZV.state == ON) callActive = true
    if (KITRH.state == ON || DENRH.state == ON || MBaRH.state == ON || MBeRH.state == ON) callActive = true
    if (wwsd_active) callActive = false

    // 3. FIRING ARBITRATION
    var double finalRate = 0.0
    var boolean update_pid_mem = false
    
    var double govOutletLimit = 100.0
    var double govFlowLimit = 100.0
    var double govPumpLimit = 100.0 
    var double pidRequest = 0.0
    var double safeRate = 0.0
    
    if (Boiler_Block_Active.state != ON && callActive && targetTemp > 0) {
            
        // --- COMPONENT 1: PID ---
        var double error = targetTemp - supplyTemp
        var double P = base_Kp * error
        pidRequest = P + mem_pid_integral
        
        if (pidRequest > 100.0) pidRequest = 100.0
        if (pidRequest < 0.0) pidRequest = 0.0
        Boiler_PID_Output.postUpdate(pidRequest)

        // --- COMPONENT 2: CASCADE OVERRIDE (High Limit Governor) ---
        val double safetyTrip = 188.0     // LOWERED TRIP POINT (Safety Buffer)
        val double modulationStart = 170.0 

        // 2. CALCULATE OVERRIDE
        govOutletLimit = 100.0

        if (outletTemp >= safetyTrip) {
            if (safety_trip_start_time == 0) safety_trip_start_time = now.millis
            if ((now.millis - safety_trip_start_time) > 5000) {
                govOutletLimit = 0.0
                logWarn("Boiler.Safety", "Approaching High Limit! Forced Shutdown at " + outletTemp)
            } else {
                govOutletLimit = 0.0 
            }
        } 
        else {
            safety_trip_start_time = 0
            if (outletTemp > modulationStart) {
                var double range = safetyTrip - modulationStart // 18.0 degrees
                var double offset = outletTemp - modulationStart
                var double ratio = offset / range 
                govOutletLimit = 100.0 - (ratio * 100.0)
                if (govOutletLimit < 0.0) govOutletLimit = 0.0
            }
        }

        // 3. DELTA T MONITOR (Passive Safety)
        // NEW: Active Delta T Policing (Pro BMS Feature)
        // If Delta T < 10F, we throttle the rate proportionally.
        // Math: DT 10F = 100% limit, DT 5F = 50% limit, DT 2F = 20% limit.
        var double boilerDeltaT = outletTemp - inletTemp
        
        if (boilerDeltaT < 10.0 && boilerDeltaT > 0.0) {
            // Throttle logic: (DT / 10) * 100
            // Example: DT = 5F -> 50% Limit
            govFlowLimit = (boilerDeltaT / 10.0) * 100.0
            if (govFlowLimit < 20.0) govFlowLimit = 20.0 // Don't strangle it completely, let min fire handle it
            logInfo("Boiler.Gov", "Delta-T Governor Active. Limiting to " + govFlowLimit.intValue + "%")
        } else {
            govFlowLimit = 100.0
        }

        // GOV C: Pump Speed Follower
        var double pump_match_target = 100.0
        if (pumpSpeed > 1.0) pump_match_target = pumpSpeed + 25.0
        else pump_match_target = 35.0
        if (pump_match_target > 100.0) pump_match_target = 100.0
        govPumpLimit = pump_match_target

        // --- ARBITRATION ---
        var double externalLimit = 100.0
        if (Boiler_Zone_Limit.state instanceof Number) externalLimit = (Boiler_Zone_Limit.state as Number).doubleValue
        
        Boiler_Gov_Outlet.postUpdate(govOutletLimit)
        Boiler_Gov_Flow.postUpdate(govFlowLimit)
        Boiler_Gov_Pump.postUpdate(govPumpLimit)

        var double proposedRate = pidRequest
        if (proposedRate > govOutletLimit) proposedRate = govOutletLimit
        if (proposedRate > govFlowLimit) proposedRate = govFlowLimit
        if (proposedRate > govPumpLimit) proposedRate = govPumpLimit
        if (proposedRate > externalLimit) proposedRate = externalLimit 
        
        // DIAGNOSTIC LOGGING
        logInfo("Boiler.Diagnostic", 
            "| Tgt:" + String::format("%.1f", targetTemp) + 
            " | Sup:" + String::format("%.1f", supplyTemp) + 
            " | Err:" + String::format("%.1f", error) + 
            " | P:" + String::format("%.1f", P) + 
            " | I:" + String::format("%.1f", mem_pid_integral) + 
            " | PID:" + String::format("%.1f", pidRequest) + 
            " | Out:" + String::format("%.1f", outletTemp) + 
            " | Gov:" + String::format("%.1f", govOutletLimit) + 
            " | DT_Gov:" + String::format("%.1f", govFlowLimit) +
            " | Final:" + String::format("%.1f", proposedRate)
        )

        // Slew Rate
        val double maxRisePerCycle = 2.0 
        if (proposedRate > lastSafeRate + maxRisePerCycle) safeRate = lastSafeRate + maxRisePerCycle
        else safeRate = proposedRate
        
        if (safeRate < 0.0) safeRate = 0.0
        if (safeRate > 100.0) safeRate = 100.0
        lastSafeRate = safeRate
        
        // --- STARTUP OVERRIDE ---
        var int desiredEnable = 0
        if (last_demand_time > 0 && (now.millis - last_demand_time) < 60000) desiredEnable = 1
        if (pidRequest > 10.0) desiredEnable = 1

        if (desiredEnable == 1 && pumpSpeed < 1.0 && safeRate < 25.0) safeRate = 25.0 

        // --- SOFT START ---
        var double ramp_base = last_firing_rate
        if (ramp_base < 20.0) ramp_base = 20.0
        if (safeRate > (ramp_base + 5.0)) safeRate = ramp_base + 5.0
        
        // --- INTEGRAL MGMT (ACTIVE GOVERNOR TRACKING) ---
        var boolean startup_freeze = (outletTemp < inletTemp + 2.0)
        var boolean governorsActive = (safeRate < pidRequest)
        
        if (!startup_freeze) {
            if (!governorsActive) {
                // Standard Integration
                mem_pid_integral = mem_pid_integral + ((Ki * 0.5) * error)
            } 
            else {
                // GOVERNOR ACTIVE: Anti-Windup
                // Back-Calculation to track the limit
                var double tracked_I = safeRate - P
                mem_pid_integral = (mem_pid_integral * 0.5) + (tracked_I * 0.5)
                logInfo("Boiler.PID", "Governor Active. Tracking I-Term down to " + String::format("%.1f", mem_pid_integral))
            }
        }
        
        if (mem_pid_integral > 50.0) mem_pid_integral = 50.0
        if (mem_pid_integral < -10.0) mem_pid_integral = -10.0
        update_pid_mem = true

        // --- DEADBAND, MIN FIRE & TAILOUT LOGIC ---
        val double min_fire = 10.0
        val double cruise_threshold = 12.0 
        val double cutoff_threshold = 5.0
        val int shutdown_debounce_ms = 10000 
        
        // TAILOUT
        // Logic: If Rate is low (<12%) AND Delta T is tiny (< 2F), we are done.
        var boolean tailout = (safeRate < cruise_threshold && boilerDeltaT < 2.0 && pumpSpeed > 10.0)

        // 1. Update VALID Demand Timer
        if (safeRate > cutoff_threshold && !tailout) {
            last_demand_time = now.millis
        }

        // 2. Determine Final Rate
        if (tailout) {
            finalRate = 0.0
            update_pid_mem = true 
            logInfo("Boiler.Tailout", "TAILOUT: Low DeltaT (" + String::format("%.1f", boilerDeltaT) + ") at Min Fire. Force Stop.")
        }
        else if (safeRate < cruise_threshold) {
            if (safeRate <= cutoff_threshold) {
                if ((now.millis - last_demand_time) < shutdown_debounce_ms) {
                    finalRate = min_fire 
                } else {
                    finalRate = 0.0 
                    update_pid_mem = true
                }
            }
            else {
                finalRate = min_fire
            }
        } else {
            finalRate = safeRate
        }
        
        if (finalRate > 100.0) finalRate = 100.0
        
        // --- EXECUTION ---
        if (finalRate > 0.1) {
            desiredEnable = 1
        } else {
            desiredEnable = 0
        }
        
        Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(desiredEnable)
        if (desiredEnable == 1) Thread::sleep(100)
        
        var int payload = (finalRate).intValue
        var int currentRate = if(Lochinvar_40003_Rate_Command.state instanceof Number) (Lochinvar_40003_Rate_Command.state as Number).intValue else -1
        
        if (currentRate != payload) {
            var int currentMode = if(Lochinvar_40001_Control_Mode.state instanceof Number) (Lochinvar_40001_Control_Mode.state as Number).intValue else 0
            if (currentMode != 5 && payload > 0) {
                 Lochinvar_40001_Control_Mode.sendCommand(5)
                 Thread::sleep(100) 
            }
            Lochinvar_40003_Rate_Command.sendCommand(payload)
        }
    } 
    else {
        finalRate = 0.0
        if (Boiler_Block_Active.state == ON) {
            mem_pid_integral = 0.0
            update_pid_mem = true
        }
        Lochinvar_40002_Boiler_Enable_Cmd.sendCommand(0)
        Lochinvar_40003_Rate_Command.sendCommand(0)
    }

    if (update_pid_mem) Boiler_Pid_Integral.postUpdate(mem_pid_integral)
    last_firing_rate = finalRate
    Boiler_Final_Command.postUpdate(finalRate)
end

// -----------------------------------------------------------------------------------
// RULE 4: GRAFANA NORMALIZATION
// -----------------------------------------------------------------------------------
rule "Normalize Boiler State for Grafana"
when
    Item Boiler_Status_Code changed or
    Item Boiler_Blocking changed or
    Item Boiler_Lockout_Code changed
then
    var int graphState = 0
    var int status = if(Boiler_Status_Code.state instanceof Number) (Boiler_Status_Code.state as Number).intValue else 0
    var int block  = if(Boiler_Blocking.state instanceof Number) (Boiler_Blocking.state as Number).intValue else 0
    var int lock   = if(Boiler_Lockout_Code.state instanceof Number) (Boiler_Lockout_Code.state as Number).intValue else 32767

    if (lock != 32767) graphState = -2
    else if (block != 0 && block != 32767) graphState = -1
    else if (status == 19) graphState = 2
    else if (status == 21 || status == 22 || status == 23) graphState = 1
    else graphState = 0

    if (Boiler_Graph_State.state != graphState) {
        Boiler_Graph_State.postUpdate(graphState)
    }
end